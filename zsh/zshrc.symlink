#!/bin/zsh
# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `p [tab]` to
export PROJECTS=~/Projects

# evaluate dircolors if it exists
if [[ `uname` == Linux ]]; then
  d=.dircolors
  test -r $d && eval "$(dircolors $d)"
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# setup antibody
antibody bundle < "$ZSH/antibody/bundles.txt"

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source "$file"
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit && compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

unset config_files

# stash your environment variables in ~/.localrc. This means they'll stay
# out of your main dotfiles repository (which may be public, like this
# one), but you'll have access to them in your scripts.
[[ -a ~/.localrc ]] && source ~/.localrc

# disable any prompt settings by $VIRTUAL_ENV/bin/activate
export VIRTUAL_ENV_DISABLE_PROMPT=yes
